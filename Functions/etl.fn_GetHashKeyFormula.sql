SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE FUNCTION [etl].[fn_GetHashKeyFormula]
(
	@SourceObject NVARCHAR (255)
)
RETURNS NVARCHAR(MAX) 
BEGIN 

--DECLARE @SourceObject NVARCHAR(255) = 'dbo.FactTicketSales'

DECLARE @Formula AS VARCHAR(MAX) = ''

SELECT @Formula = @Formula +
' ISNULL(' +
CASE
	WHEN NUMERIC_PRECISION IS NOT NULL THEN 'CONVERT(NVARCHAR(60), [' + COLUMN_NAME + '], 128)'
	WHEN DATA_TYPE LIKE 'datetime%' THEN 'CONVERT(NVARCHAR(30), [' + COLUMN_NAME + '],21)'
	WHEN DATA_TYPE = 'DATE' THEN 'CONVERT(NVARCHAR(8), [' + COLUMN_NAME + '], 112)'
	WHEN DATA_TYPE = 'TIME' THEN 'CONVERT(NVARCHAR(20), [' + COLUMN_NAME + '])'
	WHEN DATA_TYPE = 'bit' THEN 'convert(NVARCHAR(1), [' + COLUMN_NAME + '])'	
	WHEN DATA_TYPE = 'uniqueidentifier' THEN 'convert(NVARCHAR(40), [' + COLUMN_NAME + '])'	
	ELSE '' + COLUMN_NAME + ''
END + ', ''DB_NULL'') COLLATE SQL_Latin1_General_CP1_CS_AS + ' 
FROM INFORMATION_SCHEMA.COLUMNS
WHERE ISNULL(CHARACTER_MAXIMUM_LENGTH, 0) >= 0 AND DATA_TYPE NOT LIKE 'BINARY'
AND TABLE_SCHEMA = PARSENAME(@SourceObject, 2) AND TABLE_NAME = PARSENAME(@SourceObject, 1)
AND COLUMN_NAME NOT LIKE 'Sync[_][_]%'
ORDER BY COLUMN_NAME

SET @Formula = 'HASHBYTES(''sha2_256'', ' + LEFT(@Formula, (LEN(@Formula) - 2)) + ')'

RETURN @Formula

END
GO
